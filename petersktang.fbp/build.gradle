plugins {
    id 'java'
    id 'scala'
    id 'application'
    id 'maven-publish'
    id 'de.fuerstenau.buildconfig'
    id 'org.openjfx.javafxplugin'
}
sourceCompatibility = 12

repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/reactivex/RxJava/"
    }
}
dependencies {
    implementation group: 'org.scala-lang', name: 'scala-library', version: '2.13.2'
    implementation group: 'org.scala-lang.modules', name: 'scala-xml_2.13', version: '1.3.0'

    implementation group: 'io.reactivex.rxjava2', name: 'rxjavafx', version: '2.11.0-RC34'
    //implementation group: 'io.reactivex.rxjava2', name: 'rxjavafx', version: '2.11.0-RC16'
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.12'

    implementation group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    implementation group: 'org.jgrapht', name: 'jgrapht-io', version: '1.3.1'
    implementation group: 'org.jgrapht', name: 'jgrapht-core', version: '1.3.1'
    implementation group: 'org.jgrapht', name: 'jgrapht-ext', version: '1.3.1'

    implementation group: 'org.apache.tinkerpop', name: 'gremlin-core', version: '3.4.3'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

buildConfig {
    appName = project.name
    version = project.version

    clsName = 'VersionAndTimestamp'
    packageName = "com.jpaulmorrison.graphics"
    charset = 'UTF-8'

    buildConfigField 'String', "DATE", "May. 23, 2020"
}

sourceSets {
    main.java.srcDirs += 'build/gen/buildconfig/src/main'
}

application {
    mainClassName = "com.jpaulmorrison.drawFBP.Main"
}

javafx {
    version = "12.0.2"
    modules = [ 'javafx.controls', 'javafx.graphics',
                'javafx.fxml']
}

task uberJar(type: Jar) {
    archiveClassifier = 'uber'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    manifest{
        attributes("Implementation-Title": project.name,
                "Implementation-Version": project.version,
                'Permissions': 'all-permissions',
                'Built-Date': LocalDateTime.now(),
                'Built-Java': System.getProperty('java.version'),
                //"Class-Path": configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
                'Main-Class': application.mainClassName
        )
    }
}


task writeNewPom {
    doLast {
        pom {
            project {
                groupId project.group
                version project.version

                inceptionYear '2016'
                licenses {
                    license {
                        name 'GNU Lesser General Public License, Version 3.0'
                        url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("$buildDir/pom.xml")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifact project.name + '-all'
            version project.version

            from components.java
        }
        mavenJava(MavenPublication) {
            pom {
                name = project.name
                description = project.description
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}